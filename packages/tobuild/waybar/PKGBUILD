# Maintainer: Alexis Rouillard <contact@arouillard.fr>

pkgname=waybar
pkgver=r2242.ebdf575d
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors (GIT)'
arch=('x86_64')
url='https://github.com/Alexays/Waybar/'
license=('MIT')
provides=('waybar')
conflicts=('waybar')
depends=(
    'gtkmm3'
    'libjsoncpp.so'
    'libinput'
    'libsigc++'
    'fmt'
    'wayland'
    'chrono-date'
    'libspdlog.so'
    'gtk-layer-shell'
    'libpulse'
    'libnl'
    'libappindicator-gtk3'
    'libdbusmenu-gtk3'
    'libmpdclient'
)
makedepends=(
    'git'
    'cmake'
    'meson'
    'scdoc' # For generating manpages
    'wayland-protocols'
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)

source=(
	"${pkgname}::git+https://github.com/Alexays/Waybar#commit=5da45ece9dd7d2738a866f115ce40e4758f4ac2b"
	keyboard_state_cpp.patch taskbar_cpp.patch)

sha1sums=('SKIP'
          'b6777673fb308d42dd7f39e76c339acf0f26d5c5'
          'e74104a6961ddfafc3c8ae6ed2589634350337a9')

prepare() {
    cd "${srcdir}/${pkgname}"
    patch --forward --strip=0 --input="${srcdir}/keyboard_state_cpp.patch"
    patch --forward --strip=0 --input="${srcdir}/taskbar_cpp.patch"
}

build() {
    cd "${srcdir}/${pkgname}"
    rm -rf "${srcdir}/build"
    meson -Drfkill=enabled -Dtests=disabled --prefix=/usr "${srcdir}/build"
    ninja -C "${srcdir}/build"
}

package() {
    DESTDIR="$pkgdir" ninja -C "${srcdir}/build" install
}
