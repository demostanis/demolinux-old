# Maintainer: Solomon Choina <shlomochoina@gmail.com>
# Co-Maintainer: Frank Tao <frank.tao@uwaterloo.ca>
pkgname=wayfire
pkgver=0.7.4
pkgrel=1
pkgdesc="3D wayland compositor"
arch=('x86_64')
url="https://github.com/ammen99/wayfire"
license=('MIT')
depends=('cairo' 'pango' 'doctest' 'freetype2' 'glm' 'nlohmann-json'
         'libdrm' 'libevdev' 'libglvnd' 'libinput' 'libjpeg'
         'libpng' 'libxkbcommon' 'libxml2' 'pixman' 'polkit'
         'pkgconf' 'seatd' 'xcb-util-errors' 'xcb-util-renderutil'
         'xcb-util-wm' 'xorg-xwayland' 'wayland' 'wayland-protocols'
         'wlroots' 'wf-config')
makedepends=('git' 'glslang' 'meson' 'ninja' 'cmake' 'vulkan-headers')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=()
options=()
source=('git+https://github.com/WayfireWM/wayfire#commit=c3dac7ee268c6a11a2cdbdd4336aa0e06eca912f'
	'scale_cpp.patch'
	'active_state.patch')
sha256sums=('SKIP'
            '6bbeeae52f2122f190825ecbd32cc512f9898c292c10a73cc1578f05459098ce'
            'b193910d3e7df10ba11982ca383b48a04a67e6b0cf32561d022c7e4af9bd9982')

prepare() {
	    cd "$srcdir/wayfire"
	    patch --forward --strip=0 --input="${srcdir}/scale_cpp.patch"
	    patch --forward --strip=1 --input="${srcdir}/active_state.patch"
}

build() {
	cd "$srcdir/wayfire"
  arch-meson \
    -Duse_system_wlroots=enabled \
    -Duse_system_wfconfig=enabled \
    build
  ninja -C build
}

package() {
	cd "$srcdir/wayfire"
	DESTDIR="$pkgdir/" ninja -C build install
  install -Dm644 wayfire.desktop "$pkgdir"/usr/share/wayland-sessions/wayfire.desktop 
  cp wayfire.ini "$pkgdir"/usr/share
	chmod +s "$pkgdir"/usr/bin/wayfire
}
