# thank you leo_sk

pkgname=wayfire
pkgver=0.7.3
pkgrel=1
pkgdesc="3D wayland compositor"
arch=(x86_64)
url=https://wayfire.org
license=(custom:MIT)
depends=(cairo 'wf-config>=0.5' libjpeg libinput wlroots)
makedepends=(meson ninja wayland-protocols glm cmake doctest)
conflicts=("${pkgname}-git")
source=("https://github.com/WayfireWM/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=(99d1d51b1db88c30de6484094fd02df39a82cbb503361e52abc1d94f8357cb5f)

build() {
    rm -rf build
    arch-meson "${pkgname}-${pkgver}" build --buildtype=debug -Duse_system_wfconfig=enabled -Duse_system_wlroots=enabled -Db_sanitize=address,undefined
    ninja -C build
}

check () {
    meson test -C build
}

package() {
    DESTDIR="${pkgdir}/" ninja -C build install
    cd "${pkgname}-${pkgver}"
    install -Dm644 wayfire.desktop "${pkgdir}/usr/share/wayland-sessions/wayfire.desktop"
    install -Dm644 wayfire.ini "${pkgdir}/usr/share/doc/${pkgname}/wayfire.ini"
    install -Dm645 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
